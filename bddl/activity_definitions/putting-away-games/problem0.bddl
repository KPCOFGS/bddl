(define (problem putting-away-games_0)
    (:domain omnigibson)

    (:objects
        board_game.n.01_1 board_game.n.01_2 board_game.n.01_3 - board_game.n.01
        desk.n.01_1 - desk.n.01
        floor.n.01_1 - floor.n.01
        packing_box.n.02_1 packing_box.n.02_2 - packing_box.n.02
        puzzle.n.02_1 puzzle.n.02_2 puzzle.n.02_3 - puzzle.n.02
        shelf.n.01_1 shelf.n.01_2 - shelf.n.01
        agent.n.01_1 - agent.n.01
    )
    
    (:init 
        (ontop board_game.n.01_1 desk.n.01_1) 
        (ontop board_game.n.01_2 floor.n.01_1) 
        (ontop board_game.n.01_3 floor.n.01_1) 
        (ontop packing_box.n.02_1 desk.n.01_1) 
        (ontop packing_box.n.02_2 floor.n.01_1) 
        (ontop puzzle.n.02_1 desk.n.01_1) 
        (ontop puzzle.n.02_2 floor.n.01_1) 
        (inside puzzle.n.02_3 packing_box.n.02_1) 
        (inroom floor.n.01_1 childs_room) 
        (inroom shelf.n.01_1 childs_room) 
        (inroom shelf.n.01_2 childs_room) 
        (inroom desk.n.01_1 childs_room) 
        (ontop agent.n.01_1 floor.n.01_1)
    )
    
    (:goal 
        (and 
            (exists 
                (?packing_box.n.02 - packing_box.n.02)
                (and 
                    (forall 
                        (?puzzle.n.02 - puzzle.n.02)
                        (inside ?puzzle.n.02 ?packing_box.n.02)
                    )
                    (forall 
                        (?board_game.n.01 - board_game.n.01)
                        (not
                            (inside ?board_game.n.01 ?packing_box.n.02)
                        )
                    )
                )
            )
            (exists 
                (?packing_box.n.02 - packing_box.n.02)
                (and 
                    (forall 
                        (?board_game.n.01 - board_game.n.01)
                        (inside ?board_game.n.01 ?packing_box.n.02)
                    )
                    (forall 
                        (?puzzle.n.02 - puzzle.n.02)
                        (not
                            (inside ?puzzle.n.02 ?packing_box.n.02)
                        )
                    )
                )
            )
            (forall 
                (?packing_box.n.02 - packing_box.n.02)
                (exists 
                    (?shelf.n.01 - shelf.n.01)
                    (ontop ?packing_box.n.02 ?shelf.n.01)
                )
            )
        )
    )
)